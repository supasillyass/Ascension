OUTER_SPRINT abaz_loc "%MOD_FOLDER%/tougher/abazigal"

DEFINE_ACTION_FUNCTION tougher_abazigal BEGIN

   LOAD_TRA "%ascension_tra_loc%/english/tougher.tra"  "%ascension_tra_loc%/%LANGUAGE%/tougher.tra"
   INCLUDE "%MOD_FOLDER%/tougher/tougher_shared.tpa"

   LAF set_marker STR_VAR marker=fin_tougher_abazigal END
   
   // Human-form Abazigal

   LAF abazigal_main END
   LAF run_once STR_VAR function=abazigal_resources END
   
   // Dragon-form Abazigal
   
   LAF abazigal_dragon END
   
   
   // area changes
   
   LAF abazigal_plot END

   // creatures

   LAF abazigal_tamah END
   LAF abazigal_salamanders END
   LAF abazigal_drakes END


END

///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////
/// Abazigal's dragon consort
///
/// ('Tamah' is also a BG1 name, but I'll leave it alone
/// as (a) she's a very minor character, and (b) 'Tamah'
/// has fairly high name recognition)
///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_tamah BEGIN

  /* CRE file
  
  Tamah's immunities:
  - 50% resistance to elemental damage
  - immunity to backstab (redundant: dragons already get it)
  - immunity to Imprisonment, petrification, polymorph, death effects, INT drain, poison, Bigby's Hand spells, and Resilient Sphere
  - 'Sarevimm'
  - usual dragon immunities delivered via dragring
  We just need to implement a few of these more consistently. 
  
  Also, Tamah doesn't get basic BG1 proficiencies (standard otherwise for dragons). I'm not 100% sure it matters but let's play it safe.
  */

  COPY "%abaz_loc%/abazdg02.cre" override
     LPF FJ_CRE_VALIDITY END
     LPF devour_brain_immunity END
     LPF polymorph_immunity END
     LPF death_immunity END
     LPF imprisonment_immunity END
     LPF poison_immunity END         
     FOR (ind=0x6e;ind<0x76;ind+=1) BEGIN
         WRITE_BYTE ind 5
     END

  /* Weapon
  
  Has a 25% chance of knocking the target back (no save, but save vs. poison at -2 to avoid brief unconsciousness)
  For consistency with Abazigal, we introduce a save for the knockback, as well as some minor damage

  Has a 50% chance of slowing target. We add icon and string.

  Also fix off-by-one errors, and add the flag to use STR bonus (currently omitted)
  */
  
  COPY "%abaz_loc%/dragpur1.itm" override
     LPF ALTER_EFFECT INT_VAR match_probability1=25 probability1=24 END // off-by-one
     LPF ALTER_EFFECT INT_VAR match_probability1=50 probability1=49 END
     LPF ALTER_EFFECT INT_VAR match_opcode=235 save_vs_poison=1 savebonus="-2" END // save vs knockback
     LPF CLONE_EFFECT INT_VAR match_opcode=235 opcode=12 parameter1=0 parameter2=0 timing=4 dicenumber=1 dicesize=8 END // knockback damage
     LPF CLONE_EFFECT INT_VAR match_opcode=40 opcode=142 parameter2=41 END
     LPF CLONE_EFFECT INT_VAR match_opcode=40 opcode=139 parameter1=14668 END
     LPF ALTER_ITEM_HEADER INT_VAR flag_strength=1 END

  /* breath weapon
  
  50% chance of sleep for 2 rounds, with no saving throw
  100% chance of 16d8 crushing damage, save for half (breath, no modifier)
  100% chance of knockback (save vs polymorph at -2 to avoid)
  
  We correct off-by-one errors and do an EE save-for-half

  */
  
  COPY "%abaz_loc%/abazdg02.spl" override
     LPF ALTER_EFFECT INT_VAR match_probability1=50 probability1=49 END
     PATCH_IF enhanced_edition BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=12 match_save_vs_breath=0 END
        LPF ALTER_EFFECT INT_VAR match_opcode=12 dicenumber=16 save_for_half=1 END
     END

  /* scripting */
  
  COMPILE "%abaz_loc%/abazdg02.baf"
END

///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////
/// Abazigal's salamanders
///
/// Slight weakening of SALGRICE (which occurs only in
/// Abazigal's lair, so it's okay to edit the general creature)
///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_salamanders BEGIN
   COPY_EXISTING "salgrice.cre" override
        LPF CRE_patch
           INT_VAR xp_value=7500
                   level=17
                   hp_max=120
                   hp_current=120
                   thac0=7
                   attacks=3
                   resist_fire="-25"
                   resist_magic_fire="-25"
                   resist_cold=125
                   resist_magic_cold=125
                   resist_electricity=50
                   save_vs_death=6
                   save_vs_wand=8
                   save_vs_poly=7
                   save_vs_breath=7
                   save_vs_spell=9
        END
   BUT_ONLY
END

///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////
/// Abazigal's drake servants
///
/// All are from-scratch. Again their names are lifted from
/// obscure characters (all from BG1); again we offer an alternative
///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_drakes BEGIN

   //////////////////////////////////////////////
   // Fire drake
   //
   // Breath weapon: 4d10 fire damage, and a lingering 2d10 1 second later;
   // save vs breath at -2 for 1/2 damage and no lingering effect
   // (here we align saves a bit more)
   //
   // Fire drake is actually our template so much of this is redundant,
   // but I think it's clearer this way
   //////////////////////////////////////////////

   COPY "%abaz_loc%/drake01.cre" "override/drake01.cre"
       LPF drake_patch INT_VAR newname=3 oldname=10474 red=255 green=0 blue=0 END
       LPF CRE_patch
          INT_VAR resist_fire=127
                  resist_magic_fire=127
                  resist_cold=0
                  resist_magic_cold=0
                  resist_electricity=50
                  resist_acid=50
                  resist_magic=25
       END
   BUT_ONLY
   
   COPY "%abaz_loc%/drake01.baf" "override/drake01.bcs"
      COMPILE_BAF_TO_BCS
      
   COPY "%abaz_loc%/drake01.spl" override
      LPF ALTER_EFFECT INT_VAR match_save_vs_breath=1 savebonus="-2" END
      PATCH_IF enhanced_edition BEGIN
         LPF DELETE_EFFECT INT_VAR match_opcode=12 match_save_vs_breath=0 END
         LPF ALTER_EFFECT INT_VAR match_opcode=12 match_timing=1 dicenumber=4 save_for_half=1 END
      END

   //////////////////////////////////////////////
   // Shadow drake
   //
   // Breath weapon: 4d10 magic damage, 1 level drained, and 2 rounds of slow;
   //
   // save vs breath at -4 for 1/2 damage and no other effects
   //
   // we add an icon and string for the slow, and a string for the energy drain.
   // on EE we implement save-for-half
   //////////////////////////////////////////////

   COPY "%abaz_loc%/drake01.cre" "override/drake02.cre"
       LPF drake_patch INT_VAR newname=4 oldname=10435 red=0 green=0 blue=0 END
       LPF CRE_patch
          INT_VAR resist_fire=50
                  resist_magic_fire=50
                  resist_cold=50
                  resist_magic_cold=50
                  resist_electricity=50
                  resist_acid=50
                  resist_magic=35
       END
   BUT_ONLY
   
   COPY "%abaz_loc%/drake01.baf" "override/drake02.bcs"
      REPLACE_TEXTUALLY drake01 drake02
      REPLACE_TEXTUALLY ~!CheckStatGT(LastSeenBy(Myself),[0-9]+,RESISTFIRE)~ ~!CheckStatGT(LastSeenBy(Myself),85,RESISTMAGIC)!CheckStatGT(LastSeenBy(Myself),75,MAGICDAMAGERESISTANCE)~
      COMPILE_BAF_TO_BCS
      
   COPY "%abaz_loc%/drake02.spl" override
      LPF CLONE_EFFECT INT_VAR match_opcode=40 opcode=139 parameter1=1280 parameter2=0 END
      LPF CLONE_EFFECT INT_VAR match_opcode=40 opcode=142 parameter1=0 parameter2=41 END
      LPF CLONE_EFFECT INT_VAR match_opcode=216 opcode=139 parameter1=41495 parameter2=0 END
      PATCH_IF enhanced_edition BEGIN
         LPF DELETE_EFFECT INT_VAR match_opcode=12 match_save_vs_breath=0 END
         LPF ALTER_EFFECT INT_VAR match_opcode=12 match_timing=9 dicenumber=4 save_for_half=1 END
      END

   //////////////////////////////////////////////
   // Ice drake
   //
   // Breath weapon: 4d10 cold damage, and a lingering 2d10 1 second later;
   // save vs breath at -2 for 1/2 damage and no lingering effect
   // (here we align saves a bit more)
   //////////////////////////////////////////////

   COPY "%abaz_loc%/drake01.cre" "override/drake03.cre"
       LPF drake_patch INT_VAR newname=5 oldname=10564 red=205 green=255 blue=255 END
       LPF CRE_patch
          INT_VAR resist_fire=0
                  resist_magic_fire=0
                  resist_cold=127
                  resist_magic_cold=127
                  resist_electricity=50
                  resist_acid=50
                  resist_magic=25
       END
   BUT_ONLY

   COPY "%abaz_loc%/drake01.baf" "override/drake03.bcs"
      REPLACE_TEXTUALLY drake01 drake03
      REPLACE_TEXTUALLY ~!CheckStatGT(LastSeenBy(Myself),[0-9]+,RESISTFIRE)~ ~!CheckStatGT(LastSeenBy(Myself),75,RESISTCOLD)~
      REPLACE_TEXTUALLY ~Range(LastSeenBy(Myself),5)~ ~Range(LastSeenBy(Myself),8)~
      COMPILE_BAF_TO_BCS

   COPY "%abaz_loc%/drake03.spl" override
      LPF ALTER_EFFECT INT_VAR match_save_vs_breath=1 savebonus="-2" END
      PATCH_IF enhanced_edition BEGIN
         LPF DELETE_EFFECT INT_VAR match_opcode=12 match_save_vs_breath=0 END
         LPF ALTER_EFFECT INT_VAR match_opcode=12 match_timing=1 dicenumber=4 save_for_half=1 END
      END

   //////////////////////////////////////////////
   // Acid drake
   //
   // Breath weapon: 4d10 acid damage, and a lingering 2d10 1 second later;
   // save vs breath at -2 for 1/2 damage and no lingering effect
   // (here we align saves a bit more and fix a missing damage block)
   //////////////////////////////////////////////

   COPY "%abaz_loc%/drake01.cre" "override/drake04.cre"
       LPF drake_patch INT_VAR newname=6 oldname=10578 red=0 green=255 blue=0 END
       LPF CRE_patch
          INT_VAR resist_fire=50
                  resist_magic_fire=50
                  resist_cold=50
                  resist_magic_cold=50
                  resist_electricity=50
                  resist_acid=100
                  resist_magic=25
       END
   BUT_ONLY
   
   COPY "%abaz_loc%/drake01.baf" "override/drake04.bcs"
      REPLACE_TEXTUALLY drake01 drake04
      REPLACE_TEXTUALLY ~!CheckStatGT(LastSeenBy(Myself),[0-9]+,RESISTFIRE)~ ~!CheckStatGT(LastSeenBy(Myself),75,RESISTACID)~
      COMPILE_BAF_TO_BCS

   COPY "%abaz_loc%/drake04.spl" override
      PATCH_IF enhanced_edition BEGIN
         LPF ALTER_EFFECT INT_VAR match_opcode=12 match_timing=1 dicenumber=4 save_vs_breath=1 save_for_half=1 END
      END ELSE BEGIN
         LPF CLONE_EFFECT INT_VAR match_opcode=12 match_timing=1 save_vs_breath=1 END
      END
      LPF ALTER_EFFECT INT_VAR match_save_vs_breath=1 savebonus="-2" END

   //////////////////////////////////////////////
   // Drake weapon
   //
   // Claw attack with an additional 1 point of magic
   // damage, and a 20% chance of knockback with brief
   // sleep (save vs poison at -2 to avoid)
   //
   // We do a little bit of saving-throw rationalising,
   // add a sleep icon, and correct an off-by-one error
   //////////////////////////////////////////////

   COPY "%abaz_loc%/drake.itm" override
         LPF ALTER_EFFECT INT_VAR match_opcode=39 savebonus="-2" END
         LPF ALTER_EFFECT INT_VAR match_probability1=20 probability1=19 END
         LPF CLONE_EFFECT INT_VAR match_opcode=39 opcode=142 parameter2=14 END
END


///////////////////////////////////////////////////////////
/// General patch for drakes
///
/// applies name, imposes drake immunities, fixes regeneration,
/// removes redundant 'drakring'; sets script equal to name;
/// recolors
///
/// drakes are immune to: entangle/web; grease; charm; stun;
/// sleep; silence; poison; they have 50% MDR, and modified
/// collision avoidance
///////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION drake_patch
   INT_VAR newname=0
           oldname=0
           red=0
           green=0
           blue=0
BEGIN
   LPF check_ini STR_VAR ini=abazigal_drake_renames RET value END
   PATCH_IF value BEGIN
      SET name=RESOLVE_STR_REF ((AT newname))
   END ELSE BEGIN
      SET name=oldname
   END
   WRITE_LONG NAME1 name
   WRITE_LONG NAME2 name
   LPF CRE_patch
      INT_VAR regeneration=0 // the vAsc files suggest 5, but drake scripts have lots of healing, in a way which wouldn't make sense if they regenerated, so I'll go with 'inactive'
              resist_magic_damage=50
      STR_VAR script_default="%DEST_RES%"
              immunity_to_spells="%WIZARD_GREASE% %DRAGON_WING_BUFFET%"
   END
   LPF poison_immunity END
   LPF charm_immunity END
   LPF entangle_web_immunity END
   LPF silence_immunity END
   LPF stun_immunity END
   LPF sleep_immunity END
   LPF ADD_CRE_EFFECT INT_VAR opcode=300 target=1 timing=9 parameter2=1 END
   REMOVE_CRE_ITEM drakring
   LPF ALTER_EFFECT INT_VAR match_opcode=51 parameter1=(red*0x10000 + green*0x100 + blue) END

END


///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////
/// Tweaks to AR6005 and its script
///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////

/*

Here I take some liberties. vAsc moves Abazigal much closer
to the entrance, I think so that he definitely gets in when 
his drakes do. But that's a fairly ugly mess cinematically
speaking, and pushes the battle towards occurring in a cramped
area rather than the expanse of the main room.
I do it by cutscene instead, sacrificing two of
Abazigal's salamanders in the process (it would be easy
enough to add them back, but they're kind of in the way
anyway).

*/

DEFINE_ACTION_FUNCTION abazigal_plot BEGIN

///////////////////////////////////////////////////////////
//  The core cutscene
///////////////////////////////////////////////////////////

<<<<<<<<  .../ascension-inline/finabct.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		MoveViewPoint([1150.819],10)
                ActionOverride(Player2,JumpToPoint([2062.816]))
		ActionOverride(Player3,JumpToPoint([2058.744]))
		ActionOverride(Player4,JumpToPoint([2106.740]))
		ActionOverride(Player5,JumpToPoint([2102.798]))
		ActionOverride(Player6,JumpToPoint([2153.759]))
		JumpToPoint([2011.772])
		ActionOverride(Player2,MoveToPoint([1528.876]))
		ActionOverride(Player3,MoveToPoint([1525.763]))
		ActionOverride(Player4,MoveToPoint([1575.759]))
		ActionOverride(Player5,MoveToPoint([1578.927]))
		ActionOverride(Player6,MoveToPoint([1577.830]))
		MoveToPoint([1501.786])
		ActionOverride(Player2,Face(W))
		ActionOverride(Player3,Face(W))
		ActionOverride(Player4,Face(W))
		ActionOverride(Player5,Face(W))
		ActionOverride(Player6,Face(W))
		Face(W)
		Wait(1)
		ActionOverride("abazigal1",StartDialogueNoSet(Player1))
END
>>>>>>>>

COMPILE ".../ascension-inline/finabct.baf"

///////////////////////////////////////////////////////////
//  Cutscene instructions in the area script
///////////////////////////////////////////////////////////

<<<<<<<< .../ascension-inline/abaz_top.baf
IF
	Global("FinEnteredArea","AR6005",1)
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("FinEnteredArea","AR6005",2)
		StartCutSceneMode()
		StartCutScene("finabct")
END
>>>>>>>>

EXTEND_TOP ar6005.bcs ".../ascension-inline/abaz_top.baf"

///////////////////////////////////////////////////////////
//  Cutspies to trigger the cutscene
///////////////////////////////////////////////////////////

COPY_EXISTING cutspy.cre "override/finabspy.cre"
   LPF DELETE_EFFECT INT_VAR match_opcode=268 END
   WRITE_ASCII 0x250 finabspy
BUT_ONLY

<<<<<<<< .../ascension-inline/finabspy.baf
IF
  Global("finenteredarea","ar6005",0)
  Detect([PC])
THEN
    RESPONSE #100
       SetGlobal("finenteredarea","ar6005",1)
END

IF
  !Global("finenteredarea","ar6005",0)
THEN
    RESPONSE #100
             DestroySelf()
END
>>>>>>>>

COMPILE ".../ascension-inline/finabspy.baf"

///////////////////////////////////////////////////////////
//  Place the cutspies (and, while we're at it, move
//  a salamander who's in the way)
///////////////////////////////////////////////////////////

COPY_EXISTING ar6005.are override
   GET_OFFSET_ARRAY act_arr ARE_V10_ACTORS
   PHP_EACH act_arr AS ind=>act_off BEGIN
      READ_SHORT (0x20+act_off) x_loc
      READ_SHORT (0x22+act_off) y_loc
      PATCH_IF ((x_loc=1574 && y_loc=701) || (x_loc=1748 && y_loc=1172) ) BEGIN
         WRITE_ASCII (0x80+act_off) finabspy
         WRITE_LONG (0x30+act_off) 0
         WRITE_ASCII act_off Abazigal_Spy (0x20)
      END
      PATCH_IF (x_loc=1217 && y_loc=593) BEGIN
         WRITE_SHORT (0x20+act_off) 958
         WRITE_SHORT (0x24+act_off) 958
         WRITE_SHORT (0x22+act_off) 662
         WRITE_SHORT (0x26+act_off) 662
      END
   END

///////////////////////////////////////////////////////////
//  Tweak Abazigal's script to remove the automatic
//  dialog, but to trigger the cutscene if he detects players
//  (this shouldn't be able to happen, it's just a precaution)
///////////////////////////////////////////////////////////

LAF DELETE_SCRIPT_BLOCK
  STR_VAR script=abazigal
          match=SelfInit
END

<<<<<<<< .../ascension-inline/abaz_cs_add.baf
IF
  Global("finenteredarea","ar6005",0)
  Detect([PC])
THEN
    RESPONSE #100
       SetGlobal("finenteredarea","ar6005",1)
END
>>>>>>>>

EXTEND_BOTTOM abazigal.bcs ".../ascension-inline/abaz_cs_add.baf"

///////////////////////////////////////////////////////////
// Create Abazigal's drakes, and Tamah (vAsc does it by script, but
// no reason not to patch them straight in to the ARE file).
// also, one salamander is in the way; move it back.
///////////////////////////////////////////////////////////

   COPY_EXISTING ar6005.are override
         LPF ADD_ARE_ACTOR
           INT_VAR loc_x=1150
                   loc_y=650
           STR_VAR name=Drake01
                   resref=drake01
                   orientation=SE
         END
         LPF ADD_ARE_ACTOR
           INT_VAR loc_x=1350
                   loc_y=550
           STR_VAR name=Drake02
                   resref=drake02
                   orientation=SSE
         END
         LPF ADD_ARE_ACTOR
           INT_VAR loc_x=1200
                   loc_y=1000
           STR_VAR name=Drake03
                   resref=drake03
                   orientation=NE
         END
         LPF ADD_ARE_ACTOR
           INT_VAR loc_x=1350
                   loc_y=1050
           STR_VAR name=Drake04
                   resref=drake04
                   orientation=NNE
         END
         LPF ADD_ARE_ACTOR
           INT_VAR loc_x=924
                   loc_y=908
           STR_VAR name=Tamah
                   resref=abazdg02
                   orientation=E
         END
   BUT_ONLY

///////////////////////////////////////////////////////////
// End the fight when the combat counter is down, not
// just when Abazigal dies
///////////////////////////////////////////////////////////

   COPY_EXISTING "ar6005.bcs" override
         DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~Dead("Abazigal")~ ~Dead("Abazigal")CombatCounter(0)~
         END
   BUT_ONLY

///////////////////////////////////////////////////////////
// Make sure salamanders et al come in (their help script is
// overwritten in a vanilla-game bug)
///////////////////////////////////////////////////////////

<<<<<<<< .../ascension-inline/deadabaz_help.baf
IF
  Global("FinEnteredArea","AR6005",2)
  !Detect([PC])
THEN
    RESPONSE #100
       MoveToObject(Player1)
END
>>>>>>>>

EXTEND_BOTTOM deadabaz.bcs ".../ascension-inline/deadabaz_help.baf"

END

////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////
////  Human-form Abazigal
////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_main BEGIN

   COPY_EXISTING "abazigal.cre" override
      LPF abazigal_cre_patch END
      LPF check_ini STR_VAR ini=tougher_abazigal_uses_sword RET value END
      PATCH_IF !value BEGIN
         REPLACE_TEXTUALLY "abaz3" "sw2h17" (8)
      END

   // 'bhaalimm' item

   LAF run_once STR_VAR function=bhaalimm END

   // scripting - EEAsc does a cunning substitution but I think for pure-AI scripts it's safer to overwrite
   COMPILE "%abaz_loc%/abaz2.baf"


END

////////////////////////////////////////////////////////////
////  Resources for human-form Abazigal
////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_resources BEGIN

/* Ascension Abazigal has a variant of 'Gram, the Sword of Grief' that has a 10% chance of doing 6 points of magic damage (drained to Abazigal) and 1d12 electrical damage,
a 15% chance of doing 3 points of magic damage (drained to Abazigal) and 1d6 points of electrical damage, and a 75% chance of doing 1 point of electrical damage. We'll read
this back into 'tougher'. On EE we'll do the drain as 1d6 hp drain
*/

    COPY_EXISTING sw2h17.itm "override/abaz03.itm"
         LPF DELETE_EFFECT INT_VAR match_opcode=12 END // remove poison effect
         LPF DELETE_EFFECT INT_VAR match_opcode=324 END // remove poison effect
         LPF ADD_ITEM_EFFECT INT_VAR type=99 opcode=12 target=2 parameter2=4*0x10000 timing=1 dicenumber=1 dicesize=12 probability1=9 END
         LPF CLONE_EFFECT INT_VAR match_opcode=12 probability2=10 probability1=24 dicesize=6 END
         LPF CLONE_EFFECT INT_VAR match_opcode=12 match_dicesize=6 probability2=25 probability1=99 dicesize=0 parameter1=1 END
         PATCH_IF !enhanced_edition BEGIN
             LPF CLONE_EFFECT INT_VAR match_opcode=12 match_dicesize=12 parameter2=64*0x10000 dicenumber=1 dicesize=6 probability1=24 drain_to_max_hp=1 END
         END ELSE BEGIN
             LPF CLONE_EFFECT INT_VAR match_opcode=12 match_dicesize=12 dicenumber=0 dicesize=0 parameter2=64*0x10000 parameter1=6 END
             LPF CLONE_EFFECT INT_VAR match_opcode=12 target=1 opcode=17 match_parameter2=64*0x10000 parameter2=0 END
             LPF CLONE_EFFECT INT_VAR match_opcode=12 match_dicesize=6 dicenumber=0 dicesize=0 parameter2=64*0x10000 parameter1=3 END
             LPF CLONE_EFFECT INT_VAR match_opcode=12 target=1 opcode=17 match_parameter1=3 match_parameter2=64*0x10000 parameter2=0 END
         END
    BUT_ONLY

/* these two spells are only actually used by *Final-5* Abazigal. (I'm including them here for convenience - it's
harmless if they're installed in 'tougher Abazigal')
 */
 
/* abaz01 - 'Shockwave' - does 10d6 electrical damage, save vs spell at -2 for half; 5d6 magic damage, no save; 5d6 electrical damage to the caster
; imposes a -2 penalty on AC for 5 rds, and uses the 'drop item' opcode, that I think experience has taught us ought to be ignored.
 I remove that opcode and, on EE, handle save-for-half.
*/

    COPY "%abaz_loc%/abaz01.spl" override
       SAY NAME1 @9
       WRITE_SHORT 0x1c 4
       LPF DELETE_EFFECT INT_VAR match_opcode=264 END
       PATCH_IF enhanced_edition BEGIN
          LPF DELETE_EFFECT INT_VAR match_opcode=12 match_parameter2=4*0x10000 match_target=2 match_save_vs_spell=0 END
          LPF ALTER_EFFECT INT_VAR match_opcode=12 match_parameter2=4*0x10000 match_target=2 save_for_half=1 dicenumber=10 END
       END

/* abaz02 - 'Unleash' - does 3d6 magic damage (no save), 6d6 electrical damage (save vs spell at -2 for half), grants Abazigal 50
hit points, and applies a wing-buffet effect.

On EE, we impose save-for-half. I also think the 50 hp is intended to be a drain-to-caster, so I'll implement it
that way on EE.
*/

    COPY "%abaz_loc%/abaz02.spl" override
       SAY NAME1 @10
       WRITE_SHORT 0x1c 4
       PATCH_IF enhanced_edition BEGIN
          LPF DELETE_EFFECT INT_VAR match_opcode=17 END
          LPF ALTER_EFFECT INT_VAR match_opcode=12 match_parameter2=64*0x10000 drain_hp_to_caster=1 END
          LPF DELETE_EFFECT INT_VAR match_opcode=12 match_parameter2=4*0x10000 match_save_vs_spell=0 END
          LPF ALTER_EFFECT INT_VAR match_opcode=12 match_parameter2=4*0x10000 dicenumber=6 END
       END





END

////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////
////  Main cre patch for (humanoid-form) Abazigal
////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////

/*
  Loses attack-speed bonus
  Loses immunity to backstab
  Gains regeneration 5hp/sec
  3-point movement bonus
  Gains immunity to timestop and to wing buffet
  Gains two uses of Greater Deathblow
*/

DEFINE_PATCH_FUNCTION abazigal_cre_patch BEGIN
    LPF CRE_patch
       INT_VAR allegiance=255
               regeneration=5
               immunity_to_timestop=1
               immunity_to_backstab=0
       STR_VAR immunity_to_opcodes=235
               add_spells="WARRIOR_GREATER_DEATHBLOW(2)"
    END
    LPF DELETE_EFFECT INT_VAR match_opcode=190 END
    LPF ADD_CRE_EFFECT INT_VAR opcode=126 target=1 timing=1 parameter1=3 END // movement bonus
    ADD_CRE_ITEM abaz03 #0 #0 #0 "unstealable&undroppable" WEAPON1 EQUIP TWOHANDED
    ADD_CRE_ITEM potn14 #0 #0 #0 "none" QITEM
END

////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////
////  Dragon-form Abazigal
////
////  No need to separate off (most) resources, as they're
////  not used by the humanoid version of Abazigal in Ascension
////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION abazigal_dragon BEGIN

/*
  MDR 65%
  movement rate bonus: 4 -> 5
  immune to abazdg02
  regeneration 5hp/sec (actually unchanged from vanilla, but of course hidden)
  xp increases
  hp goes up to 500
  thac0 goes from -6 to -10
  50% resistance to physical damage (was 40%)
  50% resistance to acid/cold/fire
  127% resistance to electricity
*/

   COPY_EXISTING dragblue.cre override
        LPF CRE_patch 
           INT_VAR xp_value=78000
                   resist_magic_damage=65
                   regeneration=5
                   hp_max=500
                   hp_current=500
                   thac0="-10"
                   resist_piercing=50
                   resist_crushing=50
                   resist_slashing=50
                   resist_missile=50
                   resist_cold=50
                   resist_magic_cold=50
                   resist_fire=50
                   resist_magic_fire=50
                   resist_acid=50
                   resist_electricity=127
           STR_VAR immunity_to_spells=abazdg02
        END
        LPF ALTER_EFFECT INT_VAR match_opcode=126 parameter1=5 END // movement
   BUT_ONLY

   /* Abazigal's attack:
     - d12, not d8
     - wing buffet effect, 40% likely (fixing an out-by-one error) save vs death at -2 to avoid (we make it consistent, averaging save penalties)
   */
   
   COPY_EXISTING dragblue.itm override
      LPF ALTER_ITEM_HEADER
         INT_VAR dicesize=12
      END
      LPF ADD_ITEM_EFFECT INT_VAR type=99 opcode=235 target=2 duration=1 parameter1=5 parameter2=2 probability1=39 savebonus="-2" END
      LPF ALTER_EFFECT INT_VAR match_opcode=235 save_vs_poison=1 END
      LPF CLONE_EFFECT INT_VAR match_opcode=235 opcode=39 parameter1=0 parameter2=0 duration=2 END
      LPF CLONE_EFFECT INT_VAR match_opcode=235 opcode=12 parameter1=0 parameter2=0 dicenumber=1 dicesize=6 timing=4 END


  // scripting
  
  COMPILE "%abaz_loc%/abazdrag.baf"


END