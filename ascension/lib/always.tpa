
	// initialise

        OUTER_SPRINT MOD_VERSION "2.0" // in due course WEIDU will do this automatically
        OUTER_SET always_initialised=1

        // include function libraries (note that 'sfo_lite' also builds and sets a few SFO files/variables)

	INCLUDE ~ascension/lib/sfo_lite.tph~ // fragments of DavidW's function library, tweaked a bit for Ascension. Live code here
	INCLUDE ~ascension/lib/ascension_function_library.tph~ // functions built just for Ascension. No live code.
        INCLUDE "ascension/lib/alter_effect.tpa"  // edited version of Cam's DELETE/ALTER/CLONE_EFFECT suite; no live code
        INCLUDE "ascension/lib/alter_script.tpa"  // DavidW's family of script-editing functions; no live code.

	// Read INI

        OUTER_SPRINT ini_to_read "%MOD_FOLDER%/ascension.ini"
        LAM SFO_read_ini_file

        /// make sure dir.ids is present and working

        COPY "%MOD_FOLDER%/lib/dir.ids" override
        COPY_EXISTING "action.ids" override
             REPLACE_TEXTUALLY "Face\*)" "Face*DIR)"
             REPLACE_TEXTUALLY "Direction\*)" "Direction*DIR)"
        BUT_ONLY

        // add a few possibly-missing IDS entries (direct from EEAsc)

        APPEND ~gtimes.ids~ ~6 ONE_ROUND~                    UNLESS ~ONE_ROUND~
        APPEND ~gtimes.ids~ ~12 TWO_ROUNDS~                    UNLESS ~TWO_ROUNDS~
        APPEND ~gtimes.ids~ ~18 THREE_ROUNDS~                    UNLESS ~THREE_ROUNDS~
        APPEND ~gtimes.ids~ ~24 FOUR_ROUNDS~                    UNLESS ~FOUR_ROUNDS~
        APPEND ~gtimes.ids~ ~30 FIVE_ROUNDS~                    UNLESS ~FIVE_ROUNDS~
        APPEND ~gtimes.ids~ ~54 NINE_ROUNDS~                    UNLESS ~NINE_ROUNDS~
        APPEND ~gtimes.ids~ ~60 TEN_ROUNDS~                     UNLESS ~TEN_ROUNDS~
        APPEND ~gtimes.ids~ ~10 TWO_MINUTES~                    UNLESS ~TWO_MINUTES~
        APPEND ~gtimes.ids~ ~45 NINE_MINUTES~                    UNLESS ~NINE_MINUTES~
        APPEND ~gtimes.ids~ ~100 TWENTY_MINUTES~                    UNLESS ~TWENTY_MINUTES~
        APPEND ~gtimes.ids~ ~300 ONE_HOUR~                    UNLESS ~ONE_HOUR~
        APPEND ~spell.ids~  ~2302 WIZARD_REMOVE_MAGIC~          UNLESS ~WIZARD_REMOVE_MAGIC~
        APPEND ~spell.ids~  ~2705 WIZARD_KHELBENS_WARDING_WHIP~ UNLESS ~WIZARD_KHELBENS_WARDING_WHIP~


        // install ToBEx on appropriate systems (for better DS management and to turn off difficulty-based damage scaling)
        
        ACTION_IF !enhanced_edition BEGIN
            LAF check_ini STR_VAR ini=skip_tobex RET value END
            ACTION_IF ( value || !(FILE_EXISTS "bgmain.exe") ) BEGIN
               OUTER_SET tobex_present=0
            END ELSE BEGIN
               OUTER_SET tobex_present=1 
               ACTION_IF !FILE_EXISTS "tobex_ini/tobextweak.ini" BEGIN // if ToBex isn't present already, install it
                 OUTER_SPRINT ~TOBEX_MOD_DIRECTORY~ ~%MOD_FOLDER%~
                 LOAD_TRA ~%MOD_FOLDER%/tobex_redist/tobex.tra~
                 INCLUDE ~%MOD_FOLDER%/tobex_redist/tobex.tpa~
                 SILENT
               END
            END
        END

        // disable difficulty-based damage scaling (code borrowed from SCS)

        // This is easy on EE (just edit the ini to trigger already-present functionality), trickier on Windows non-EE (it needs
        // ToBEX) and impossible on non-Windows version of vanilla BG2

        LAF check_ini STR_VAR ini="enable_difficulty_based_damage_changes" RET value END
        ACTION_IF !value BEGIN
            ACTION_IF enhanced_edition BEGIN
                 ACTION_IF FILE_EXISTS "%USER_DIRECTORY%/baldur.lua" BEGIN
                       COPY "%USER_DIRECTORY%/baldur.lua" "%USER_DIRECTORY%"
                            REPLACE_TEXTUALLY ~SetPrivateProfileString('Game Options','Suppress Extra Difficulty Damage','0')~ ~SetPrivateProfileString('Game Options','Suppress Extra Difficulty Damage','1')~
                       BUT_ONLY
                 END
            END ELSE BEGIN
                ACTION_IF tobex_present BEGIN // are we on ToBEx? If not, do nothing
                        // adjust to switch off difficulty
      <<<<<<<< .../ascension-inline/diffmod.2da
2DA V1.0
0
	DAMAGE_MOD	LUCK_MOD
EASIEST	0               0
EASY	0               0
NORMAL	0	        0
HARD	0	        0
HARDEST	0	        0
      >>>>>>>>
                    COPY ".../ascension-inline/diffmod.2da" override
               END
           END
      END



        // mark this version of Ascension as having working DS (some versions of DS try to fix Ascension)

        ACTION_IF !FILE_EXISTS_IN_GAME ~dw#fixasc~ BEGIN
          COPY_EXISTING ~misc01.itm~ ~override/dw#fixasc~
        END

	// read in all spell variable names
	
	LAM read_in_spell_scroll_resrefs

        // install DS if not already present

        ACTION_IF !FILE_EXISTS ~%marker_loc%/dw#asc_ds.mrk~ BEGIN
           COPY_EXISTING "sw1h01.itm" "%marker_loc%/dw#asc_ds.mrk"
           INCLUDE "%MOD_FOLDER%/ds/ds.tph"
           LAF detectable_spells END
        END
        CLEAR_IDS_MAP

        // Ascension-specific detections
        
        INCLUDE "%MOD_FOLDER%/ds/ds_extra.tpa"
        LAF run_once STR_VAR function=ds_extra END

	// handle EE charset issues
	
	SILENT
	LAF check_ini STR_VAR ini=force_tra_rebuild RET force_tra_rebuild=value END
        LAF handle_charsets_immutably INT_VAR force_tra_rebuild STR_VAR tra_base=lang RET ascension_tra_loc=scs_tra_loc END

	// collect a list of markers
	
	LAF audit_markers RET_ARRAY marker_list END
	
	// if appropriate, install the testing suite
	
	LAF check_ini STR_VAR ini=testing_suite RET value END
	ACTION_IF value BEGIN
	   INCLUDE "%MOD_FOLDER%/lib/testing_suite.tpa"
           LAF run_once STR_VAR function=testing_suite END
        END

        VERBOSE
